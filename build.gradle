apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'
apply plugin: 'com.gradle.plugin-publish'

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.15.0'
    }
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}



group = 'com.dorongold.plugins'
version = '2.1.0'
description = "Gradle plugin that adds a 'taskTree' task that prints task dependency tree"

//Building a text file that contains the classpath of the plugin code. needed for testing on gradle versions prior to 2.5
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'org.apache.commons:commons-lang3:3.5'

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile 'com.netflix.nebula:nebula-test:5.0.1'
    testCompile 'commons-io:commons-io:2.5'
    testRuntime files(createClasspathManifest)
}

pluginBundle {
    website = 'https://github.com/dorongold/gradle-task-tree'
    vcsUrl = 'https://github.com/dorongold/gradle-task-tree.git'
    vcsUrl = 'https://github.com/dorongold/gradle-task-tree.git'
    description = "Gradle plugin that adds a 'taskTree' task that prints task dependency tree"
    tags = ['task', 'tree', 'dependencies', 'dependency']

    plugins {
        taskTreePlugin {
            id = 'com.dorongold.task-tree'
            displayName = 'Gradle Task Tree Plugin'
        }
    }
}

test {
    testLogging {
//        showStandardStreams = true
        exceptionFormat = 'full'
    }
}
