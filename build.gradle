apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: "com.gradle.plugin-publish"

sourceCompatibility = 1.7

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.10.0"
    }
}

repositories {
    jcenter()
}

group = 'com.dorongold.plugins'
version = '1.5'
description = "Gradle plugin that adds a 'taskTree' task that prints task dependency tree"

//Building a text file that contains the classpath of the plugin code. needed for testing on gradle versions prior to 2.5
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'org.apache.commons:commons-lang3:3.5'

    testCompile gradleTestKit()
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile 'com.netflix.nebula:nebula-test:5.0.1'
    testRuntime files(createClasspathManifest)
}

pluginBundle {
    website = 'https://github.com/dorongold/gradle-task-tree'
    vcsUrl = 'https://github.com/dorongold/gradle-task-tree.git'
    description = "Gradle plugin that adds a 'taskTree' task that prints task dependency tree"
    tags = ['task', 'tree', 'dependencies', 'dependency']

    plugins {
        taskTreePlugin {
            id = 'com.dorongold.task-tree'
            displayName = 'Gradle Task Tree Plugin'
        }
    }
}

test {
    testLogging {
//        showStandardStreams = true
        exceptionFormat = 'full'
    }
}
